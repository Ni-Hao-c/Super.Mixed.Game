untyped // for ent.Fire() things
global function Tesla_Core_Init

global function OnCoreCharge_Tesla_Core
global function OnCoreChargeEnd_Tesla_Core

global function OnAbilityStart_Tesla_Core

// consts
const int DAMAGE_AGAINST_TITANS 			= 550
const int DAMAGE_AGAINST_PILOTS 			= 30

const float EMP_DAMAGE_TICK_RATE 			= 0.5

const asset FX_EMP_FIELD					= $"P_xo_emp_field"
const asset FX_EMP_FIELD_1P					= $"P_body_emp_1P"

// archon usage
const int DAMAGE_AGAINST_TITANS_ARCHON 		= 400 // archon have main weapon upgrade, so empfield deal less damage

void function Tesla_Core_Init()
{
    RegisterSignal( "TeslaCoreEnd" ) // to end doomedstate tracks
#if SERVER
	// can't use titanEMPField as damageSourceID, since client will do most stuffs
	// adding a new damageSourceId. it's gonna transfer to client automatically
	RegisterWeaponDamageSource( "mp_titancore_tesla_core", "Tesla Launcher" )
	RegisterWeaponDamageSource( "mp_titancore_tesla_arc_field", "Tesla Arc" ) // "Tesla Arc Field", limited to 1 space-bar usage...
	AddDamageCallbackSourceID( eDamageSourceId.mp_titancore_tesla_arc_field, TeslaCoreDamagedTarget )
#endif
}

bool function OnCoreCharge_Tesla_Core( entity weapon )
{
	if ( !OnAbilityCharge_TitanCore( weapon ) )
		return false

	return true
}

void function OnCoreChargeEnd_Tesla_Core( entity weapon )
{
#if SERVER
	OnAbilityChargeEnd_TitanCore( weapon )
#endif
}

var function OnAbilityStart_Tesla_Core( entity weapon, WeaponPrimaryAttackParams attackParams )
{
	entity owner = weapon.GetWeaponOwner()
	if ( !owner.IsTitan() )
		return 0
	entity soul = owner.GetTitanSoul()
	if ( !IsValid( soul ) )
		return
	#if SERVER
	float duration = weapon.GetWeaponSettingFloat( eWeaponVar.charge_cooldown_delay )
	thread TeslaCoreThink( weapon, duration )

	OnAbilityStart_TitanCore( weapon )
	#endif

	return 1
}

void function TeslaCoreThink( entity weapon, float coreDuration )
{
#if SERVER
	weapon.EndSignal( "OnDestroy" )
	entity owner = weapon.GetWeaponOwner()
	owner.EndSignal( "OnDestroy" )
	owner.EndSignal( "OnDeath" )
	owner.EndSignal( "DisembarkingTitan" )
	owner.EndSignal( "TitanEjectionStarted" )
	owner.EndSignal( "TeslaCoreEnd" )
	owner.EndSignal( "OnSyncedMeleeVictim" )

	if( !owner.IsTitan() )
		return

    if ( owner.IsPlayer() )
    {
        EmitSoundOnEntityOnlyToPlayer( owner, owner, "Titan_Ronin_Sword_Core_Activated_Upgraded_1P" )
        EmitSoundOnEntityExceptToPlayer( owner, owner, "Titan_Ronin_Sword_Core_Activated_Upgraded_3P" )
    }
    else // npc
    {
        EmitSoundOnEntity( owner, "Titan_Ronin_Sword_Core_Activated_Upgraded_3P" )
    }

	entity soul = owner.GetTitanSoul()
	int statusEffect = StatusEffect_AddEndless( soul, eStatusEffect.damageAmpFXOnly, 1.0 ) // add a visual effect
	
	bool archonVersion = weapon.HasMod( "archon_tesla" )
	thread TeslaCoreEMPFieldThink( owner, archonVersion )
	if ( archonVersion )
		thread ArchonMainWeaponSuperChargedThink( owner, coreDuration )
    //thread EMPTitanThinkConstant( owner ) // start empField
	// using a modified empField function now!
    //thread TrackEMPTitanDoomedState( owner ) // vanilla empField will end after titan doomed, let's do a check here

	OnThreadEnd(
	function() : ( weapon, soul, owner, statusEffect )
		{
			if ( IsValid( owner ) )
			{
				StopSoundOnEntity( owner, "Titan_Ronin_Sword_Core_Activated_Upgraded_1P" )
                StopSoundOnEntity( owner, "Titan_Ronin_Sword_Core_Activated_Upgraded_3P" )

				// defensive fix
				StopSoundOnEntity( owner, "EMP_Titan_Electrical_Field" )
				if ( IsValid( owner.GetTitanSoul() ) )
					EnableTitanRodeo( owner ) //Make the arc titan rodeoable now that it is no longer electrified.

                if ( owner.IsPlayer() )
                {
				    EmitSoundOnEntityOnlyToPlayer( owner, owner, "Titan_Ronin_Sword_Core_Deactivated_1P" )
                    EmitSoundOnEntityExceptToPlayer( owner, owner, "Titan_Ronin_Sword_Core_Deactivated_3P" )
                }
                else // npc
                {
                    EmitSoundOnEntity( owner, "Titan_Ronin_Sword_Core_Deactivated_3P" )
                }
                
                if ( owner.IsPlayer() )
				{
                    owner.Signal( "TeslaCoreEnd" )
					owner.Signal( "StopEMPField" ) // the signal to stop empField
					StatusEffect_Stop( owner, statusEffect )
				}
			}

			if ( IsValid( weapon ) )
			{
				if ( IsValid( owner ) )
					CoreDeactivate( owner, weapon )
				OnAbilityEnd_TitanCore( weapon )
			}

			if ( IsValid( soul ) )
			{
				CleanupCoreEffect( soul )
				StatusEffect_Stop( soul, statusEffect )
			}
		}
	)

	wait coreDuration
#endif
}

#if SERVER
// using a modified empField function now!
/*
void function TrackEMPTitanDoomedState( entity titan )
{
    titan.EndSignal( "TeslaCoreEnd" ) // if core ends this won't run

    titan.WaitSignal( "Doomed" ) // wait for titan being doomed
    thread EMPTitanThinkConstant( titan ) // re-start empField
}
*/

// main mechanic goes here, from _ai_emp_titans.gnut
void function TeslaCoreEMPFieldThink( entity titan, bool isArchon = false )
{
	titan.EndSignal( "OnDeath" )
	titan.EndSignal( "OnDestroy" )
	titan.EndSignal( "StopEMPField" )
	titan.EndSignal( "TeslaCoreEnd" )

	//We don't want pilots accidently rodeoing an electrified titan.
	DisableTitanRodeo( titan )

	//Used to identify this titan as an arc titan
	//SetTargetName( titan, "empTitan" )

	if ( HasSoul( titan ) )
	{
		entity soul = titan.GetTitanSoul()
		soul.EndSignal( "StopEMPField" )
	}

	string attachment = "hijack"

	int attachID = titan.LookupAttachment( attachment )

	EmitSoundOnEntity( titan, "EMP_Titan_Electrical_Field" )

	array<entity> particles = []

	//emp field fx
	vector origin = titan.GetAttachmentOrigin( attachID )
	if ( titan.IsPlayer() )
	{
		entity particleSystem = CreateEntity( "info_particle_system" )
		particleSystem.kv.start_active = 1
		particleSystem.kv.VisibilityFlags = ENTITY_VISIBLE_TO_OWNER
		particleSystem.SetValueForEffectNameKey( FX_EMP_FIELD_1P )

		particleSystem.SetOrigin( origin )
		particleSystem.SetOwner( titan )
		DispatchSpawn( particleSystem )
		particleSystem.SetParent( titan, attachment )
		particles.append( particleSystem )
	}

	entity particleSystem = CreateEntity( "info_particle_system" )
	particleSystem.kv.start_active = 1
	if ( titan.IsPlayer() )
		particleSystem.kv.VisibilityFlags = (ENTITY_VISIBLE_TO_FRIENDLY | ENTITY_VISIBLE_TO_ENEMY)	// everyone but owner
	else
		particleSystem.kv.VisibilityFlags = ENTITY_VISIBLE_TO_EVERYONE
	particleSystem.SetValueForEffectNameKey( FX_EMP_FIELD )
	particleSystem.SetOwner( titan )
	particleSystem.SetOrigin( origin )
	DispatchSpawn( particleSystem )
	particleSystem.SetParent( titan, attachment )
	particles.append( particleSystem )

	if (!titan.IsPlayer())
	{
		titan.SetDangerousAreaRadius( ARC_TITAN_EMP_FIELD_RADIUS )
	}

	OnThreadEnd(
		function () : ( titan, particles )
		{
			if ( IsValid( titan ) )
			{
				StopSoundOnEntity( titan, "EMP_Titan_Electrical_Field" )
				if ( IsValid( titan.GetTitanSoul() ) )
					EnableTitanRodeo( titan ) //Make the arc titan rodeoable now that it is no longer electrified.
			}

			foreach ( particleSystem in particles )
			{
				if ( IsValid_ThisFrame( particleSystem ) )
				{
					particleSystem.ClearParent()
					particleSystem.Fire( "StopPlayEndCap" )
					particleSystem.Kill_Deprecated_UseDestroyInstead( 1.0 )
				}
			}
		}
	)

	wait 0.5 // first wait here, for fx being setup
	int damage = DAMAGE_AGAINST_PILOTS
	int damageHeavyArmor = DAMAGE_AGAINST_TITANS
	if ( isArchon )
		damageHeavyArmor = DAMAGE_AGAINST_TITANS_ARCHON
	float interval = EMP_DAMAGE_TICK_RATE

	while ( true )
	{
		origin = titan.GetAttachmentOrigin( attachID )

		RadiusDamage(
			origin,									// center
			titan,									// attacker
			titan,									// inflictor
			damage,									// damage
			damageHeavyArmor,						// damageHeavyArmor
			ARC_TITAN_EMP_FIELD_INNER_RADIUS,		// innerRadius
			ARC_TITAN_EMP_FIELD_RADIUS,				// outerRadius
			SF_ENVEXPLOSION_NO_DAMAGEOWNER,			// flags
			0,										// distanceFromAttacker
			DAMAGE_AGAINST_PILOTS,					// explosionForce
			DF_ELECTRICAL | DF_STOPS_TITAN_REGEN,	// scriptDamageFlags
			eDamageSourceId.mp_titancore_tesla_arc_field )			// scriptDamageSourceIdentifier

		wait interval
	}
}

// damage, took from cl_ai_emp_titans.gnut
const string ARC_TITAN_TITAN_SCREEN_SFX 	= "Titan_Offhand_ElectricSmoke_Titan_Damage_1P"
const string ARC_TITAN_PILOT_SCREEN_SFX 	= "Titan_Offhand_ElectricSmoke_Human_Damage_1P"

const float ARC_TITAN_EMP_DURATION			= 0.35
const float ARC_TITAN_EMP_FADEOUT_DURATION	= 0.35
const float ARC_TITAN_SCREEN_EFFECTS 		= 0.085
// no idea why respawn uses client script to handle it...
void function TeslaCoreDamagedTarget( entity victim, var damageInfo )
{
	if ( !IsAlive( victim ) )
		return
	if ( !victim.IsPlayer() )
		return

	entity attacker = DamageInfo_GetAttacker( damageInfo )
	if ( !IsAlive( attacker ) )
		return

	if ( DamageInfo_GetDamage( damageInfo ) <= 0 )
		return

	if ( DamageInfo_GetCustomDamageType( damageInfo ) & DF_DOOMED_HEALTH_LOSS )
		return

	int attachID 	= attacker.LookupAttachment( "hijack" )
	vector origin 	= attacker.GetOrigin() + < 0, 0, 80 >
	if ( attachID > 0 )
		origin		= attacker.GetAttachmentOrigin( attachID )
	float distSqr 	= DistanceSqr( origin, victim.GetOrigin() )

	float minDist 	= ARC_TITAN_EMP_FIELD_INNER_RADIUS_SQR
	float maxDist 	= ARC_TITAN_EMP_FIELD_RADIUS_SQR
	float empFxHigh = ARC_TITAN_SCREEN_EFFECTS
	float empFxLow 	= ( ARC_TITAN_SCREEN_EFFECTS * 0.6 )
	float screenEffectAmplitude = GraphCapped( distSqr, minDist, maxDist, empFxHigh, empFxLow )

	if ( victim.IsTitan() )
	{
		Remote_CallFunction_Replay( victim, "ServerCallback_TitanEMP", screenEffectAmplitude, ARC_TITAN_EMP_DURATION, ARC_TITAN_EMP_FADEOUT_DURATION )
		EmitSoundOnEntityOnlyToPlayer( victim, victim, ARC_TITAN_TITAN_SCREEN_SFX )
	}
	else
	{
		EmitSoundOnEntityOnlyToPlayer( victim, victim, ARC_TITAN_PILOT_SCREEN_SFX )
	}
}

// archon tesla core
void function ArchonMainWeaponSuperChargedThink( entity owner, float coreDuration )
{
	// same signals as empField does, so it can get exact duration
	owner.EndSignal( "OnDeath" )
	owner.EndSignal( "OnDestroy" )
	owner.EndSignal( "StopEMPField" )
	owner.EndSignal( "TeslaCoreEnd" )

	entity chargeWeapon
	foreach ( entity weapon in owner.GetMainWeapons() )
	{
		if ( weapon.HasMod( "storm_launcher" ) )
		{
			chargeWeapon = weapon
			break
		}
	}

	if ( !IsValid( chargeWeapon ) ) // don't have a storm launcher
		return
	if ( chargeWeapon.HasMod( "tesla_core_charged" ) ) // already has a super charged! don't do logic below
		return


	table results = {}
	results.weapon <- chargeWeapon
	results.starterClipMod <- ""
	results.clipUpgrade <- ""

	OnThreadEnd
	(
		function(): ( results )
		{
			entity weapon = expect entity( results.weapon )
			if ( IsValid( weapon ) ) // weapon still valid, we clean up mods
			{
				array<string> mods = weapon.GetMods()
				string modToGive = expect string( results.starterClipMod ) // get starter mod
				string modToRemove = expect string( results.clipUpgrade ) // get upgrade
				mods.removebyvalue( "tesla_core_charged" ) // remove super charged
				if ( modToRemove != "" )
					mods.removebyvalue( modToRemove )
				if ( modToGive != "" )
					mods.append( modToGive )

				weapon.SetMods( mods ) // reset to starter mods
			}
		}
	)

	array<string> mods = chargeWeapon.GetMods()
	string starterClipMod = ""
	string clipUpgrade = ""
	if ( chargeWeapon.HasMod( "fd_wpn_upgrade_1" ) ) // 2 clip at start
	{
		starterClipMod = "fd_wpn_upgrade_1"
		clipUpgrade = "fd_wpn_upgrade_2" // upgrade to 3
	}
	else if ( chargeWeapon.HasMod( "fd_wpn_upgrade_2" ) ) // 3 clip at start
	{
		starterClipMod = "fd_wpn_upgrade_2"
		clipUpgrade = "" // don't upgrade
	}
	else // 1 clip at start
	{
		starterClipMod = ""
		clipUpgrade = "fd_wpn_upgrade_1" // upgrade to 2
	}
	// add it to weapon
	if ( starterClipMod != "" )
		mods.removebyvalue( starterClipMod )
	if ( clipUpgrade != "" )
		mods.append( clipUpgrade )

	// for later we clean up
	results.starterClipMod <- starterClipMod
	results.clipUpgrade <- clipUpgrade

	mods.append( "tesla_core_charged" ) // add charged mod
	chargeWeapon.SetMods( mods ) // update mod

	wait coreDuration
}
#endif // SERVER